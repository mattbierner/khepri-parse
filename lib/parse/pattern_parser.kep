/**
 * @fileOverview Khepri patterns parsers.
 */
package (
    listPattern
    listPattern0
    
    pattern
    unpack
    topLevelPattern
    
    identifierPattern
    operatorPattern
    sinkPattern
    ellipsisPattern
    
    importPattern
    arrayPattern
    
    objectPatternElement
    objectPattern
    
    asPattern)
with
    import 'bennu::parse' {
        attempt
        append
        bind
        choice
        eager
        either
        expected
        enumeration
        next
        optional
        label
        late},
    import 'bennu::lang' {
        between
        sepBy
        sepBy1
        then},
    
    import 'khepri-ast::pattern' ast_pattern,
    
    import './common' {
        node
        nodea
        sepEndWith1
        sepEndWith
        sepEndWith0},
    import './token_parser' {keyword punctuator},
    import './value_parser' {
        identifier
        operator
        stringLiteral}
in {

/* Forward
 ******************************************************************************/
topLevelPattern = late\-> { return topLevelPattern; };

unpack = late\-> { return unpack; };

asPattern = late\-> { return asPattern; };

/* Parts
 ******************************************************************************/
var sep = optional (punctuator ',');

/* Helpers
 ******************************************************************************/
listPattern = \pre mid post  ->
    append(
        sepEndWith(sep, mid, pre),
        next(sep, sepBy(sep, post)));

listPattern0 = \pre mid post  ->
    append(
        sepEndWith0(sep, mid, pre),
        next(sep, sepBy(sep, post)));

/* Base Patterns
 ******************************************************************************/
identifierPattern = label@'Identifier Pattern' <|
    identifier
        .map\x -> ast_pattern.IdentifierPattern.create(x.loc, x);

operatorPattern = label@'Operator Pattern' <|
    operator
        .map\x -> ast_pattern.IdentifierPattern.create(x.loc, x);

sinkPattern = label@'Sink Pattern' <|
    keyword('_')
        .map\x -> ast_pattern.SinkPattern.create(x.loc);

/* Ellipsis Pattern
 ******************************************************************************/
ellipsisPattern = label@'Ellipsis Pattern' <|
    either(
        attempt <| between(punctuator '(', punctuator ')',
            punctuator '...')
            .map\x -> ast_pattern.EllipsisPattern.create(x.loc, null),
        
        node(
            next(
                punctuator '...',
                optional identifierPattern),
            ast_pattern.EllipsisPattern.create));

/* Array Pattern
 ******************************************************************************/
arrayPattern = label@'Array Pattern' <| let
    element = topLevelPattern
in
    nodea(
        enumeration(
            optional(false, punctuator '?'),
            between(punctuator '[', punctuator ']',
                eager listPattern(
                    element |> expected@"array pattern element",
                    ellipsisPattern,
                    element |> expected@"non-ellipsis array pattern element"))),
        \loc checked elements ->
            ast_pattern.ArrayPattern.create(loc, elements, checked));

/* Object Pattern
 ******************************************************************************/
objectPatternElement = either(
    nodea(
        enumeration(
            stringLiteral,
            next(
                punctuator(':', '#'),
                unpack)),
        ast_pattern.ObjectPatternElement.create),
    node(
        either(
            asPattern,
            identifierPattern),
        ast_pattern.ObjectPatternElement.create));

objectPattern = label@'Object Pattern' <|
    nodea(
        enumeration(
            optional(false, punctuator '?'),
            between(punctuator '{', punctuator '}',
                eager sepBy1(sep,
                    objectPatternElement |> expected@"object pattern element"))),
        \loc checked elements ->
            ast_pattern.ObjectPattern.create(loc, elements, checked));

/* As Pattern
 ******************************************************************************/
asPattern = label@'As Pattern' <|
    nodea(
        enumeration(
            attempt then(
                identifierPattern,
                punctuator '#'),
            choice(
                attempt arrayPattern,
                objectPattern)
                |> expected@'object or array pattern'),
        ast_pattern.AsPattern.create);

/* Import Pattern
 ******************************************************************************/
importPattern = label@'Import Pattern' <|
    next(
        keyword 'import',
        nodea(
            enumeration(
                stringLiteral,
                topLevelPattern),
            ast_pattern.ImportPattern.create));

/* Patterns
 ******************************************************************************/
unpack = label@'Unpack' <|
    choice(
        attempt arrayPattern,
        objectPattern,
        asPattern,
        identifierPattern,
        operatorPattern);

topLevelPattern = label@'Top Level Pattern' <|
    choice(
        sinkPattern,
        unpack);

}