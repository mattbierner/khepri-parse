/**
 * @fileOverview Khepri expression parsers.
 */
package (
    arrayLiteral
    objectLiteral
    
    operatorExpression
    
    letExpression
    
    primaryExpression
    
    accessor
    memberExpression
    
    newExpression
    curryExpression
    applicationExpression

// Unary Expression
    unaryOperator
    unaryExpression
    
// Binary Expression
    binaryExpression
    
// Conditional Expression
    conditionalExpression
    
// Assignment
    leftHandReferenceExpression
    assignmentExpression

// Expression
    expression
    topLevelExpression)
with
    import 'bennu::parse' {
        always
        append
        attempt
        bind
        binds
        choice
        cons
        eager
        either
        enumeration
        expected
        many
        many1
        memo
        next
        optional
        rec
        token
        late
        label},
    import 'bennu::lang' {
        between
        chainl1
        sepBy
        sepBy1
        then},
    import 'nu-stream::stream' {foldl foldr reduce},
    
    import 'khepri-ast::declaration' ast_declaration,
    import 'khepri-ast::expression' ast_expression,
    import 'khepri-ast::value' ast_value,
    import 'khepri-ast::position' {SourceLocation},
    
    import './common' {node nodea precedence},
    import './token_parser' tokenParser#{
        keyword
        punctuator
        prefixedOp},
    import './value_parser' value#{
        identifier
        literal
        operator
        stringLiteral
        numericLiteral},
    import './pattern_parser' {topLevelPattern},
    import './function_parser' _
in {

/* Circular Declarations 
 ******************************************************************************/
var functionExpression := late \->
    with import './function_parser' {functionExpression} in {
        return functionExpression;
    };


/* Forward Declarations
 ******************************************************************************/
expression = late \-> { return expression; };

memberExpression = late \-> { return memberExpression; };

operatorExpression = late \-> { return operatorExpression; };

accessor = late \-> { return accessor; };

curryExpression = late \-> { return curryExpression; };

/* Components
 ******************************************************************************/
// args
////////////////////////////////////////
var args := label@'Arguments' <| let
    arg := expression
in
    node(
        between(punctuator '(', punctuator ')',
            eager <| sepBy(punctuator ',',
                arg |> expected@"argument")),
        \loc x -> {
            x.loc := loc;
            return x;
        });

/* SubExpression
 ******************************************************************************/
// Array Literal
////////////////////////////////////////
arrayLiteral := label@'Array Literal' <| let
    arrayElement := expression,
    
    arrayElements =
        sepBy(punctuator ',',
            arrayElement |> expected@"array element")
in
    node(
        between(punctuator '[', punctuator ']',
            eager arrayElements),
        ast_expression.ArrayExpression.create);

// Object Literal
////////////////////////////////////////
objectLiteral := label@'Object Literal' <| let
    propertyName := choice(
        stringLiteral,
        numericLiteral,
        identifier),
    
    propertyInitializer := label@'Property Initializer' <|
        nodea(
            enumeration(
                then(
                    propertyName,
                    punctuator ':'),
                expression),
            ast_value.ObjectValue.create),
    
    objectProperties =
        sepBy(punctuator ',',
            propertyInitializer |> expected@"object property")
in
    node(
        between(punctuator '{', punctuator '}',
            eager objectProperties),
        ast_expression.ObjectExpression.create);

// Let Expression
////////////////////////////////////////
var letBinding := label@'Let Binding' <|
    nodea(
        enumeration(
            topLevelPattern |> expected@'pattern',
            punctuator('=', '=:', ':='),
            expression |> expected@'bound value'),
       \loc pattern rec expr ->
            ast_declaration.Binding.create(loc,
                pattern,
                expr,
                (rec.value === ':=')));

letExpression := label@'Let Expression' <| let
    letBindings =
        sepBy1(punctuator ',',
            letBinding),
    
    letBody := expression
in
    nodea(
        next(
            keyword 'let',
            enumeration(
                eager letBindings |> expected@'let bindings',
                next(
                    keyword 'in',
                    letBody |> expected@'let body expression'))),
        ast_expression.LetExpression.create);

// Conditional Expression
////////////////////////////////////////
conditionalExpression := label@'Conditional Expression' <|
    nodea(
        next(
            punctuator '?',
            enumeration(
                expression,
                next(
                    punctuator ':',
                    expression
                    |> expected@"conditional consequent expression"),
                next(
                    punctuator ':',
                    expression
                    |> expected@"conditional alternate expression"))),
        ast_expression.ConditionalExpression.create);

// ArgumentList
////////////////////////////////////////
var argumentList := label@'Argument List' <|
    either(
        attempt node(
            operatorExpression,
            \loc x -> {
                var a := [x];
                a.loc := loc;
                return a;
            }),
        args);

// Dot Expression
////////////////////////////////////////
/**
 * @TODO: code dup
 */
var dotExpression := let
    reducer := \p c ->
        ?c.hasOwnProperty('property')
            :ast_expression.MemberExpression.create(
                ?p : SourceLocation.merge(p.loc, c.loc) : c.loc,
                p,
                c.property,
                c.computed)
            :ast_expression.CallExpression.create(
                ?p : SourceLocation.merge(p.loc, c.loc) : c.loc,
                p,
                c)
in
    bind(
        cons(
            accessor,
            many <| either(argumentList, accessor)),
        foldl@(reducer, null) \> always);

// Operators Expression
////////////////////////////////////////
var unaryOperatorExpression := label@'Unary Operator Expression' <|
    choice(
        value.unaryOperator,
        
        keyword('typeof', 'void').map \ {loc value} ->
            ast_value.UnaryOperator.create(loc, value),
        
        attempt  dotExpression)
    .map \ op ->
        ast_expression.OperatorExpression.create(op.loc, op);

var binaryOperatorExpression := label@'Binary Operator Expression' <|
    nodea(
        cons(
            optional keyword('_'),
            either(
                enumeration(
                    either(
                        keyword 'new',
                        punctuator '.'),
                    optional next(punctuator ',', expression)),
                enumeration(
                    choice(
                        keyword 'instanceof',
                        punctuator '@',
                        tokenParser.binaryOperator),
                    optional next(
                        optional punctuator(','),
                        expression)))),
        \loc flipped op arg -> let
            operator := ast_expression.OperatorExpression.create(loc,
                ast_value.BinaryOperator.create(loc, op.value),
                flipped)
        in
            ?arg
                :ast_expression.CurryExpression.create(loc,
                    operator,
                    [arg])
                :operator);

var ternayOperatorExpression := label@'Ternary Operator Expression' <|
    punctuator('?')
        .map \ {loc value} ->
            ast_expression.OperatorExpression.create(loc,
                ast_value.TernaryOperator.create(loc, value));

operatorExpression := label@'Operator Expression' <|
    between(punctuator '(', punctuator ')',
        choice(
            unaryOperatorExpression,
            binaryOperatorExpression,
            ternayOperatorExpression));


// Primary Expression
////////////////////////////////////////
primaryExpression := label@'Primary Expression' <|
    choice(
        letExpression,
        conditionalExpression,
        identifier,
        literal,
        arrayLiteral,
        objectLiteral,
        functionExpression,
        attempt operatorExpression,
        dotExpression.map \ expr ->
            ast_expression.OperatorExpression.create(expr.loc, expr),
        between(punctuator '(', punctuator ')',
            expression |> expected@"expression"));


// Accessor
////////////////////////////////////////
var accessorTarget := either(
    identifier.map \ x -> [x, false],
    between(punctuator '(', punctuator ')',
        expression |> expected@"accessor expression").map \x -> [x, true]);

accessor := label@'Accessor' <|
    node(
        next(
            punctuator '.',
            accessorTarget),
        \loc [x computed] -> ({
            'loc': loc,
            'property': x,
            'computed': computed
        }));

// New Expression
////////////////////////////////////////
newExpression := label@'New Expression' <|
    nodea(
        next(
            keyword 'new',
            enumeration(
                memberExpression |> expected@"new object",
                either(
                    args,
                    eager <| enumeration(curryExpression))
                    |> expected@"argument list")),
        ast_expression.NewExpression.create);

// Member Expression
////////////////////////////////////////
var accessorReducer := \p c -> 
    ast_expression.MemberExpression.create(
        SourceLocation.merge(p.loc, c.loc),
        p,
        c.property,
        c.computed);

memberExpression := label@'Member Expression' <|
    binds(
        enumeration(
            either(
                primaryExpression,
                newExpression),
            many <| memo accessor),
        foldl@accessorReducer \>> always);

// Lefthand side Expression
////////////////////////////////////////
var leftHandSideExpression := label@'Call Expression' <| let
    reducer := \p c ->
        ?c.hasOwnProperty('property')
            :ast_expression.MemberExpression.create(
                SourceLocation.merge(p.loc, c.loc),
                p,
                c.property,
                c.computed,
                c.checked)
            :ast_expression.CallExpression.create(
                SourceLocation.merge(p.loc, c.loc),
                p,
                c)
in
    binds(
        enumeration(
            memberExpression,
            many <| either(argumentList, accessor)),
        foldl@reducer \>> always);

// Curry Expression
////////////////////////////////////////
curryExpression := label@'Curry Expression' <| let
    reducer := \f args ->
        ast_expression.CurryExpression.create(
            SourceLocation.merge(f.loc, args.loc),
            f,
            [].concat(args))
in
    binds(
        enumeration(
            leftHandSideExpression,
            many next(
                punctuator '@',
                either(
                    argumentList,
                    leftHandSideExpression)
                |> expected@'curry argument')),
        foldl@reducer \>> always);

// Application Expression
////////////////////////////////////////
applicationExpression := label@'Call Expression' <|
    chainl1(
        always \p c ->
            ast_expression.CallExpression.create(
                SourceLocation.merge(p.loc, c.loc),
                p,
                [c]),
        curryExpression);

// Unary Expression
////////////////////////////////////////
unaryOperator := label@'Unary Operator' <|
    either(
        keyword('typeof', 'void'),
        tokenParser.unaryOperator);

unaryExpression := label@'Unary Expression' <| let
    reducer := \argument op -> 
        ast_expression.UnaryExpression.create(
            SourceLocation.merge(op.loc, argument.loc),
            ast_value.UnaryOperator.create(op.loc, op.value),
            argument)
in
    binds(
        enumeration(
            many unaryOperator,
            applicationExpression |> expected@"unary argument"),
        \ops expression ->
            always(foldr(reducer, expression, ops)));

// Binary Expressions
////////////////////////////////////////
var createBinary := \loc op l r ->
    ast_expression.BinaryExpression.create(loc,
        ast_value.BinaryOperator.create(op.loc, op.value),
        l,
        r);

var precedenceTable := [
    {
        'sep': prefixedOp '??',
        'precedence': 0,
        'node': createBinary
    },
    {
        'sep': prefixedOp('*', '/', '%'),
        'precedence': 1,
        'node': createBinary
    }, {
        'sep': prefixedOp('+', '-'),
        'precedence': 2,
        'node': createBinary
    }, {
        'sep': prefixedOp('<<', '>>', '>>>'),
        'precedence': 3,
        'node': createBinary
    }, {
        'sep': either(
            prefixedOp('<', '>'),
            keyword 'instanceof'),
        'precedence': 4,
        'node': createBinary
    }, {
        'sep': prefixedOp('==', '!='),
        'precedence': 5,
        'node': createBinary
    }, {
        'sep': prefixedOp '&',
        'precedence': 6,
        'node': createBinary
    }, {
        'sep': prefixedOp '^',
        'precedence': 7,
        'node': createBinary
    }, {
        'sep': prefixedOp '|',
        'precedence': 8,
        'node': createBinary
    }, {
        'sep': prefixedOp('\\>', '\\>>'),
        'precedence': 9,
        'right': true,
        'node': createBinary
    }, {
        'sep': prefixedOp('<\\', '<<\\'),
        'precedence': 9,
        'node': createBinary
    }, {
        'sep': prefixedOp('|>', '|>>'),
        'precedence': 10,
        'node': createBinary
    }, {
        'sep': prefixedOp('<|', '<<|'),
        'precedence': 10,
        'right': true,
        'node': createBinary
    }, {
        'sep': prefixedOp '||',
        'precedence': 11,
        'node': createBinary
    }, {
        'sep': prefixedOp '&&',
        'precedence': 12,
        'node': createBinary
    }
];

binaryExpression := label@'Binary Expression' <|
    precedence(
        memo unaryExpression,
        precedenceTable);

// Expression
////////////////////////////////////////
expression := binaryExpression;

// Left Hand Reference Expression
////////////////////////////////////////
leftHandReferenceExpression := label@'Left Hand Reference Expression' <|
    either(
        operator,
        binds(
            enumeration(
                identifier,
                many <| memo accessor),
            foldl@accessorReducer \>> always));

var leftHandMemberReference := label@'Left Hand Reference Expression' <|
    binds(
        enumeration(
            identifier,
            many1 <| memo accessor),
        foldl@accessorReducer \>> always);

// Assignment Expression
////////////////////////////////////////
assignmentExpression := label@'Assignment Expression' <| rec\self ->
    nodea(
        append(
            attempt enumeration(
                leftHandReferenceExpression,
                punctuator('=', ':=')),
            enumeration(
                either(self, expression) |> expected@'expression')),
        \loc left op right ->
            ast_expression.AssignmentExpression.create(loc, left, right, op.value === ':='));

// Delete Expression
////////////////////////////////////////
var deleteExpression := label@'Delete Expression' <|
    node(
        next(
            keyword 'delete',
            leftHandMemberReference |> expected@'reference expression'),
        ast_expression.DeleteExpression.create);

// Top Level Expression
////////////////////////////////////////
topLevelExpression := choice(
    deleteExpression,
    assignmentExpression,
    expression);

}